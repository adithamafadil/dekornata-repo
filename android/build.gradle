buildscript {
    ext.build_tools_version = "30.0.3"
    ext.kotlin_version = '1.5.21'
    ext.min_sdk_version = 21
    ext.target_sdk_version = 30
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.2.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects { subproject ->
    afterEvaluate {
        if ((subproject.plugins.hasPlugin("android") || subproject.plugins.hasPlugin("android-library"))) {
            android {
                compileSdkVersion rootProject.ext.target_sdk_version
                buildToolsVersion rootProject.ext.build_tools_version

                defaultConfig {
                    minSdkVersion rootProject.ext.min_sdk_version
                    targetSdkVersion rootProject.ext.target_sdk_version
                }
            }
        }
    }
    
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(":app")
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
